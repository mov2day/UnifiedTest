plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'io.github.mov2day'
version = '0.3.2'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gradlePlugin {
    website.set('https://github.com/mov2day/UnifiedTest')
    vcsUrl.set('https://github.com/mov2day/UnifiedTest.git')
    
    plugins {
        unifiedTestAgent {
            id = 'io.github.mov2day.unifiedtest'
            implementationClass = 'io.github.mov2day.unifiedtest.UnifiedTestAgentPlugin'
            displayName = 'UnifiedTest'
            description = 'Advanced test automation observability and reporting for JUnit4, JUnit5, and TestNG.'
            tags.set(['test', 'report', 'observability', 'junit', 'testng', 'automation'])
        }
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

configurations {
    implementation {
        resolutionStrategy {
            force 'com.google.code.gson:gson:2.10.1'
        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    implementation 'org.junit.platform:junit-platform-launcher:1.10.2'
    implementation 'junit:junit:4.13.2'
    implementation 'org.testng:testng:7.10.2'
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.google.code.gson:gson:2.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.10.0'
    testImplementation 'net.bytebuddy:byte-buddy:1.14.12'
    testImplementation 'net.bytebuddy:byte-buddy-agent:1.14.12'
    testImplementation gradleTestKit()
}

test {
    useJUnitPlatform()
}

tasks.register('publishAll') {
    group = 'publishing'
    description = 'Publishes to both Gradle Plugin Portal and Maven repository.'
    dependsOn 'publishPlugins', 'publish'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'unifiedtest'
            version = project.version
            pom {
                name = 'UnifiedTest'
                description = 'UnifiedTest Gradle plugin for unified test reporting and management.'
                url = 'https://mov2day.github.io/UnifiedTest/'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'mov2day'
                        name = 'Muthu Kumar Koodalingam'
                        email = 'mov2day@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mov2day/UnifiedTest.git'
                    developerConnection = 'scm:git:ssh://github.com:mov2day/UnifiedTest.git'
                    url = 'https://github.com/mov2day/UnifiedTest'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = findProperty('ossrhUsername') ?: System.getenv('OSSRH_USERNAME')
                password = findProperty('ossrhPassword') ?: System.getenv('OSSRH_PASSWORD')
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
